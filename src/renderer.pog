
/*
    Grax library will concern itself with
        - OpenGL boilerplate
        - immediate rendering
            - draw basic 2D shapes
            - draw text
            - draw images/sprites
        - loading of assets: textures and models


    TODO:
    - rect rendering
*/


Shader default3d_shader;
Shader default2d_shader;

UBO* camera_ubo;
UBO* application_ubo;

Texture2D white_texture;

GLFWwindow* main_window;
float32 main_window_width = 1600;
float32 main_window_height = 900;
float32 main_window_aspect; // height / width

float64 time;
float64 deltatime;
float64 sim_speed = 1.0;

vec2 window_size() {
    int32 w;
    int32 h;
    glfwGetWindowSize(main_window, *w, *h);
    return {w, h} as vec2;
}


bool grax_loop() {

    dispatch_immediate();

    glfwSwapBuffers(main_window);
    input_state_reset();
    glfwPollEvents();

    if glfwWindowShouldClose(main_window) {
        glfwDestroyWindow(main_window);
        glfwTerminate();
        return false;
    }

    // begin next frame:

    input_update();

    deltatime = (1.0 / 60.0) * sim_speed;
    time += deltatime;

    glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);

    { // update application UBO
        struct app {
            float32 time, delta, width, height;
        }
        app a;
        a.time = time as float32;
        a.delta = deltatime as float32;
        a.width = main_window_width;
        a.height = main_window_height;
        update_buffer(application_ubo.buffer_id, sizeof app, *a);
    }

    return true;
}

void grax_init() {

    if !glfwInit() {
        print("[ERROR]: failed to initilize glfw.\n");
        return;
    }

    // glfwWindowHint(GLFW_DECORATED, GLFW_FALSE);

    glfwWindowHint(GLFW_CONTEXT_VERSION_MAJOR, 4);
    glfwWindowHint(GLFW_CONTEXT_VERSION_MINOR, 6);
    glfwWindowHint(GLFW_OPENGL_PROFILE, GLFW_OPENGL_CORE_PROFILE);

    main_window = glfwCreateWindow(main_window_width as int32, main_window_height as int32, "Grax", null, null);
    main_window_aspect = main_window_height / main_window_width;
    if !main_window {
        glfwTerminate();
        print("[ERROR]: failed to initilize main_window.\n");
        return;
    }

    glfwMakeContextCurrent(main_window);

    // glfw callbacks
    glfwSetFramebufferSizeCallback(main_window, on_resize);
    glfwSetScrollCallback(main_window, on_scroll);

    load_opengl(glfwGetProcAddress);
    print("[INFO]: OpenGL version: ", glGetString(GL_VERSION) as char*, "\n");

    if true {
        glEnable(GL_DEBUG_OUTPUT);
        glEnable(GL_DEBUG_OUTPUT_SYNCHRONOUS);
        glDebugMessageCallback(opengl_debug_callback, null);
        print("[INFO]: OpenGL synchronous debug messages enabled.\n");
    }

    enable_alpha_blending();
    enable_depth_test();
    glEnable(GL_CULL_FACE);
    glClearColor(0.1, 0.1, 0.1, 1.0);

    camera_ubo = get_ubo("Camera");
    application_ubo = get_ubo("Application");

    init_immediate_renderer();

    {
        // TODO: the "../grax/" here is a hack!
        default3d_shader = load_shader("../grax/shaders/default3d.glsl");
        default2d_shader = load_shader("../grax/shaders/default2d.glsl");
    }

    { // init white texture
        Color pixel = { 255, 255, 255, 255 };
        Image image = { width = 1, height = 1, pixels = *pixel };
        white_texture = create_texture2D(image);
    }
}

void on_resize(GLFWwindow* window, int32 w, int32 h) {

    if w == 0 or h == 0  return;

    main_window_width = w as float32;
    main_window_height = h as float32;
    main_window_aspect = main_window_height / main_window_width;

    print("[INFO]: Window resize: ", w, "x", h, "\n");

    glViewport(0, 0, w, h);

    resize(g_buffer, w as uint32, h as uint32);
    resize(hdr_buffer, w as uint32, h as uint32);
}

void opengl_debug_callback(GLenum source, GLenum _type, GLuint id, GLenum severity, GLsizei length, /*const*/ GLchar* message, /*const*/ void* userParam) {

    switch (_type) {
        case GL_DEBUG_TYPE_ERROR: print("ERROR: "); break;
        case GL_DEBUG_TYPE_DEPRECATED_BEHAVIOR: print("DEPRECATED BEHAVIOR: "); break;
        case GL_DEBUG_TYPE_UNDEFINED_BEHAVIOR: print("UDEFINED BEHAVIOR: "); break;
        case GL_DEBUG_TYPE_PORTABILITY: print("PORTABILITY: "); break;
        case GL_DEBUG_TYPE_PERFORMANCE: print("PERFORMANCE: "); break;
        case GL_DEBUG_TYPE_OTHER: return; // print("OTHER: "); break;
        case GL_DEBUG_TYPE_MARKER: print("MARKER: "); break;
    }

    print(message, "\n");
}

GLFWmonitor* get_ideal_monitor(GLFWwindow* window) {

    int32 x;
    int32 y;
    int32 w;
    int32 h;
    glfwGetWindowPos(window, *x, *y);
    glfwGetWindowSize(window, *w, *h);

    int32 monitor_count;
    GLFWmonitor** monitors = glfwGetMonitors(*monitor_count);

    int32 ideal = 0;
    int32 highest_area = 0;
    for i : 0 .. monitor_count {
        GLFWmonitor* m = monitors[i];

        int32 mX;
        int32 mY;
        glfwGetMonitorPos(m, *mX, *mY);
        GLFWvidmode* mode = glfwGetVideoMode(m);

        int32 area = max(0, min(x + w, mX + mode.width ) - max(x, mX))
                   * max(0, min(y + h, mY + mode.height) - max(y, mY));

        if area > highest_area {
            highest_area = area;
            ideal = i;
        }
    }

    return monitors[ideal];
}

void toggle_fullscreen(GLFWwindow* window) {
    GLFWmonitor* monitor = glfwGetWindowMonitor(window);
    if (monitor) {
        // set to windowed
        int32 mX;
        int32 mY;
        glfwGetMonitorPos(monitor, *mX, *mY);
        // TODO: calc better values here instead of +60, for example we could center the window, or remember its last position
        glfwSetWindowMonitor(window, null, mX + 60, mY + 60, 1600, 900, 0);

    } else {
        // set to fullscreen
        GLFWmonitor* m = get_ideal_monitor(window);
        GLFWvidmode* mode = glfwGetVideoMode(m);
        glfwSetWindowMonitor(window, m, 0, 0, mode.width, mode.height, mode.refreshRate);
    }
}


void enable_alpha_blending() {
    glEnable(GL_BLEND);
    glBlendEquation(GL_FUNC_ADD);
    glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);
}

void enable_additive_blending() {
    glEnable(GL_BLEND);
    glBlendEquation(GL_FUNC_ADD);
    glBlendFunc(GL_ONE, GL_ONE);
}

void disable_blending() { glDisable(GL_BLEND); }

void enable_depth_test() { glEnable(GL_DEPTH_TEST); }
void disable_depth_test() { glDisable(GL_DEPTH_TEST); }


void set_clear_color(Color color) { glClearColor(color.r / 255.0, color.g / 255.0, color.b / 255.0, color.a / 255.0); }
void set_clear_depth(float64 depth) { glClearDepth(depth); }

void clear_color() { glClear(GL_COLOR_BUFFER_BIT); }
void clear_color(Color color) { set_clear_color(color); clear_color(); }

void clear_depth() { glClear(GL_DEPTH_BUFFER_BIT); }
void clear_depth(float64 depth) { set_clear_depth(depth); glClear(GL_DEPTH_BUFFER_BIT); }