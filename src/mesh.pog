

Vertex_Attrib_Type[] vertex3D_attribs = { .vec3, .vec3, .vec2 };
struct vertex3D {
    vec3 pos;
    vec3 normal;
    vec2 uv;
}

Vertex_Attrib_Type[] vertex2D_attribs = { .vec2, .vec2, .color };
struct vertex2D {
    vec2 pos;
    vec2 uv;
    Color color;
}



struct Mesh {
    vertex3D[..] vertices;
    uint32[..] indices;

    vec3 bb_min, bb_max;
    float32 max_radius;
}

Mesh mesh_create() {
    Mesh m = {};
    m.vertices = list_create(sizeof vertex3D);
    m.indices  = list_create(sizeof uint32);
    return m;
}

void mesh_free(Mesh m) {
    list_delete(m.vertices);
    list_delete(m.indices);
}

void add_vert(Mesh* m, vertex3D v) list_add(*m.vertices, *v);
void add_vert(Mesh* m, vec3 p) {
    vertex3D v = {};
    v.pos = p;
    add_vert(m, v);
}

void add_ind(Mesh* m, uint32 i) list_add(*m.indices, *i);

void add_tri(Mesh* m, uint32 i0, i1, i2) {
    list_add(*m.indices, *i0);
    list_add(*m.indices, *i1);
    list_add(*m.indices, *i2);
}

void add_tri(Mesh* m, vertex3D v0, v1, v2) {
    uint32 c = list_length(m.indices);
    add_tri(m, c+0, c+1, c+2);
    add_vert(m, v0);
    add_vert(m, v1);
    add_vert(m, v2);
}

void add_tri(Mesh* m, vec3 v0, v1, v2) {
    uint32 c = list_length(m.indices);
    add_tri(m, c+0, c+1, c+2);
    add_vert(m, v0);
    add_vert(m, v1);
    add_vert(m, v2);
}

struct DrawBuffers {
    uint32 vao, vbo, ebo;
    int32 elements_count;
}



enum Vertex_Attrib_Type {
    vec2; vec3; vec4;
    color; float32;
}

struct Vertex_Attribute_Info {
    GLenum _type;
    int32 count;
    uint32 bytesize;
    bool normalized;
}

Vertex_Attribute_Info get_vertex_attribute_info(Vertex_Attrib_Type datatype) {
    switch datatype {
        case .float32: return { _type = GL_FLOAT,         count = 1, bytesize = 4,  normalized = false };
        case .vec2:    return { _type = GL_FLOAT,         count = 2, bytesize = 8,  normalized = false };
        case .vec3:    return { _type = GL_FLOAT,         count = 3, bytesize = 12, normalized = false };
        case .vec4:    return { _type = GL_FLOAT,         count = 4, bytesize = 16, normalized = false };
        case .color:   return { _type = GL_UNSIGNED_BYTE, count = 4, bytesize = 4,  normalized = true  };
    }

    return {};
}

uint32 set_vertex_attribute_pointer(uint32 index, Vertex_Attrib_Type datatype, int32 stride, uint32 offset, divisor) {
    let info = get_vertex_attribute_info(datatype);

    glEnableVertexAttribArray(index);
    glVertexAttribPointer(index, info.count, info._type, info.normalized, stride, offset);
    glVertexAttribDivisor(index, divisor);

    return info.bytesize;
}

void init_vertex_layout(int32 stride, Vertex_Attrib_Type[] attribs) return init_vertex_layout(0, 0, stride, attribs);
void init_vertex_layout(uint32 base_index, divisor, int32 stride, Vertex_Attrib_Type[] attribs) {
    uint32 offset = 0;
    for uint32 index : 0 .. attribs.length {
        offset += set_vertex_attribute_pointer(base_index + index, attribs[index], stride, offset, divisor);
    }
}


DrawBuffers create_draw_buffers(Mesh mesh) {
    return create_draw_buffers(sizeof vertex3D, vertex3D_attribs,
                                list_length(mesh.vertices) * sizeof vertex3D, mesh.vertices as void*,
                                list_length(mesh.indices), mesh.indices as uint32*);
}

DrawBuffers create_draw_buffers(vertex3D[] verts, uint32[] inds) {
    return create_draw_buffers(sizeof vertex3D, vertex3D_attribs,
                                verts.length * sizeof vertex3D, verts.data as void*,
                                inds.length, inds.data as uint32*);
}

DrawBuffers create_draw_buffers(int32 vertex_size, Vertex_Attrib_Type[] attribs) return create_draw_buffers(vertex_size, attribs, 0, null, 0, null);

DrawBuffers create_draw_buffers(int32 vertex_size, Vertex_Attrib_Type[] attribs,
                                uint32 vbo_bytes, void* vbo_data,
                                uint32 elements_count, uint32* ebo_data) // TODO: glBufferData hint should be an argument here
{
    DrawBuffers db = {};
    db.elements_count = elements_count;

    glGenVertexArrays(1, *db.vao);
    glBindVertexArray(db.vao);

    glGenBuffers(1, *db.vbo);
    glBindBuffer(GL_ARRAY_BUFFER , db.vbo);
    glGenBuffers(1, *db.ebo);
    glBindBuffer(GL_ELEMENT_ARRAY_BUFFER, db.ebo);

    if vbo_data glBufferData(GL_ARRAY_BUFFER, vbo_bytes, vbo_data, GL_STATIC_DRAW);
    if ebo_data glBufferData(GL_ELEMENT_ARRAY_BUFFER, elements_count * sizeof uint32, ebo_data, GL_STATIC_DRAW);

    init_vertex_layout(vertex_size, attribs);

    glBindVertexArray(0);
    glBindBuffer(GL_ARRAY_BUFFER, 0);
    return db;
}

void update_buffers(with DrawBuffers* db, Mesh mesh) {
    update_indices(db, list_length(mesh.indices), mesh.indices as uint32*);
    update_vertices(db, list_length(mesh.vertices) * sizeof vertex3D, mesh.vertices as void*);
}

void update_buffers(with DrawBuffers* db, vertex2D[] vertices, uint32[] indices) { update_buffers(db, vertices.data, vertices.length, indices.data, indices.length); }
void update_buffers(with DrawBuffers* db, vertex2D* vertices, uint32 vertices_count, uint32* indices, uint32 indices_count) {
    db.elements_count = indices_count as int32;
    update_buffer(db.vbo, vertices_count * sizeof vertex2D, vertices);
    update_buffer(db.ebo, indices_count * sizeof uint32, indices);
}

void update_vertices(with DrawBuffers* db, uint32 vbo_bytes, void* vertices) update_buffer(db.vbo, vbo_bytes, vertices);

void update_indices(with DrawBuffers* db, uint32[] indices) update_indices(db, indices.length, indices.data);
void update_indices(with DrawBuffers* db, uint32 count, uint32* indices) {
    db.elements_count = count;
    update_buffer(db.ebo, count * sizeof uint32, indices);
}

void draw_elements(with DrawBuffers db) {
    glBindVertexArray(db.vao);
    glDrawElements(GL_TRIANGLES, db.elements_count, GL_UNSIGNED_INT, null);
    glBindVertexArray(0);
}

void draw_elements(with DrawBuffers db, uint32 instanceCount) {
    glBindVertexArray(db.vao);
    glDrawElementsInstanced(GL_TRIANGLES, db.elements_count, GL_UNSIGNED_INT, null, instanceCount);
    glBindVertexArray(0);
}

void draw_elements_lines(with DrawBuffers db) {
    glBindVertexArray(db.vao);
    glDrawElements(GL_LINES, db.elements_count, GL_UNSIGNED_INT, null);
    glBindVertexArray(0);
}



void update_buffer(uint32 buffer, void[..] list) update_buffer(buffer, list_length(list) * list_stride(list), list as void*);
void update_buffer(uint32 buffer, uint32 size, void* data) {
    glBindBuffer(GL_ARRAY_BUFFER, buffer);
    glBufferData(GL_ARRAY_BUFFER, size, data, GL_STATIC_DRAW);
    glBindBuffer(GL_ARRAY_BUFFER, 0);
}


DrawBuffers load_drawable(char* name) {
    OBJ_Data obj = load_obj(name);
    // Mesh mesh = obj_to_flatshaded_mesh(obj);
    Mesh mesh = obj_to_mesh(obj);
    free_obj(obj);
    gen_normals(mesh);
    DrawBuffers db = create_draw_buffers(mesh);
    mesh_free(mesh);
    return db;
}


void calc_bounds(Mesh* mesh) {
    vec3 bb_min = mesh.vertices[0].pos;
    vec3 bb_max = mesh.vertices[0].pos;
    let sql = sqlength(mesh.vertices[0].pos);

    uint32 count = list_length(mesh.vertices);
    for 1 .. count {
        let v = mesh.vertices[it];
        bb_min = min(bb_min, v.pos);
        bb_max = max(bb_max, v.pos);

        let s = sqlength(v.pos);
        if sql < s  sql = s;
    }

    mesh.bb_min = bb_min;
    mesh.bb_max = bb_max;
    mesh.max_radius = sqrtf(sql);
}

void gen_normals(Mesh mesh) {
    // set normals to zero
    for 0 .. list_length(mesh.vertices)  mesh.vertices[it].normal = {0, 0, 0};

    // add every triangles contribution to every vertex normal
    uint32 i = 0; // TODO: we really need to add traditional for loops
    while i < list_length(mesh.indices) {
        uint32 i0 = mesh.indices[i];
        uint32 i1 = mesh.indices[i + 1];
        uint32 i2 = mesh.indices[i + 2];

        vec3 v0 = mesh.vertices[i0].pos;
        vec3 v1 = mesh.vertices[i1].pos;
        vec3 v2 = mesh.vertices[i2].pos;

        vec3 normal = cross(sub(v0, v2), sub(v1, v2));

        // TODO: += operator overload would make this significantly neater
        mesh.vertices[i0].normal = add(mesh.vertices[i0].normal, normal);
        mesh.vertices[i1].normal = add(mesh.vertices[i1].normal, normal);
        mesh.vertices[i2].normal = add(mesh.vertices[i2].normal, normal);

        i += 3;
    }

    // normalize normals
    for 0 .. list_length(mesh.vertices)  mesh.vertices[it].normal = normalize(mesh.vertices[it].normal);
}

void flip_indices(Mesh mesh) { flip_indices(mesh, 0, list_length(mesh.indices)); }
void flip_indices(Mesh mesh, uint32 base, uint32 len) {
    uint32 i = 0; // TODO: we really need to add traditional for loops
    while i < len {
        let t = mesh.indices[base + i];
        mesh.indices[base + i] = mesh.indices[base + i + 2];
        mesh.indices[base + i + 2] = t;
        i += 3;
    }

    // for uint32 i = 0, i < len, i += 3 {}
}

void set_origin(Mesh mesh, vec3 origin) {
    for 0 .. list_length(mesh.vertices) {
        mesh.vertices[it].pos = mesh.vertices[it].pos - origin;
    }
}

vec3 avg_vertex_pos(Mesh mesh) {
    vec3 avg = vec3_zero;
    uint32 len = list_length(mesh.vertices);
    for 0 .. len {
        avg = avg + mesh.vertices[it].pos;
    }
    return avg / len as float32;
}

Mesh gen_plane(uint32 size) { return gen_plane(size, 1.0, 1.0); }
Mesh gen_plane(uint32 size, float32 uv_scale, scale) {
    let m = mesh_create();
    add_plane(*m, size, uv_scale, make_mat4(scale));
    return m;
}


void add_plane(Mesh* mesh, uint32 size, float32 uv_scale, mat4 matrix) {
    let vsize = size + 1;
    float32 half_size = size as float32 / 2;
    uint32 start_index = list_length(mesh.vertices);

    for uint32 xi: 0 .. vsize  for uint32 yi: 0 .. vsize {
        vertex3D v = {};
        v.pos = { (xi - half_size), 0, (yi - half_size) };
        v.normal = vec3_unit_y;
        v.uv = { xi as float32 / size * uv_scale, yi as float32 / size * uv_scale };

        v.pos = transform_point(v.pos, matrix);
        v.normal = transform_dir(v.normal, matrix);

        add_vert(mesh, v);

        if xi < size and yi < size {
            uint32 vi = start_index + xi * vsize + yi;
            add_tri(mesh, vi, vi + 1, vi + vsize + 1);
            add_tri(mesh, vi, vi + vsize + 1, vi + vsize);
        }
    }
}
