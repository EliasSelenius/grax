
/*
    Relevant articles by Glenn Fiedler:
        integration:             https://gafferongames.com/post/integration_basics/
        integrating orientation: https://gafferongames.com/post/physics_in_3d/
        overview:                https://gafferongames.com/categories/game-physics/

*/

struct PhysicsProperties {
    float32 cof_s; // coefficient of static friction
    float32 cof_k; // coefficient of kinetic friction
    float32 cod;   // coefficient of drag
    float32 cor;   // coefficient of restitution
}

struct Rigidbody {
    float32 mass;
    PhysicsProperties props;
    vec3 velocity;
    vec3 angular_velocity;
}

vec3 get_tangential_velocity(vec3 ang_vel, p) {
    return cross(p, ang_vel);
}

void add_velocity(with Rigidbody* rb, vec3 vel, offset) {

}

void add_force(with Rigidbody* rb, vec3 force) {
    rb.velocity = rb.velocity + force * (1.0 / rb.mass);
}

void add_force(with Rigidbody* rb, vec3 force, offset) {
    add_force(rb, force);
    // TODO: cross(force, offset) is angular acceleration, not torque?
    add_torque(rb, cross(force, offset));
}

void add_torque(with Rigidbody* rb, vec3 torque) {
    // TODO: mass / inertia ?
    rb.angular_velocity = rb.angular_velocity + torque;
}

void update_rigidbody_transform(with Transform* tr, with Rigidbody* rb, float32 dt) {
    tr.position = tr.position + rb.velocity * dt;

    let axis = rb.angular_velocity;
    let angle = length(axis);
    if angle != 0 {
        axis = axis * (1.0 / angle); // normalizes
        rotate(tr, axis, angle * dt);
    }
}

vec3 calc_dragforce(vec3 vel, float32 cod, fluid_density) {
    float32 dragforce_mag = 0.5 * fluid_density * cod * sqlength(vel);
    vec3    dragforce = neg(noz(vel)) * dragforce_mag;
    return dragforce;
}

vec3 calc_new_vel(vec3 vel, normal, float32 mass, e, cof_s, cof_k) {
    float32 perp_speed = dot(vel, normal);
    vec3 perp = normal * perp_speed; // perpendicular velocity
    vec3 tang = vel - perp;          // tangential velocity

    vec3 new_perp = normal * -e * perp_speed;

    float32 tang_speed = length(tang);
    if tang_speed < 0.000001  return new_perp;

    float32 normal_impulse = mass * (1 + e) * abs(perp_speed);
    float32 tangental_impulse = mass * tang_speed;

    float32 static_friction  = cof_s * normal_impulse;
    float32 dynamic_friction = cof_k * normal_impulse;

    if tangental_impulse <= static_friction  return new_perp;

    vec3 new_tang = noz(tang) * max(0, tang_speed - dynamic_friction / mass);
    return new_tang + new_perp;
}





// enum CollisonShape {
//     Sphere; Point; Capsule; Cube; Triangle;
// }

// struct Collider {
//     CollisonShape shape;
//     vec3 A, B, C;
//     float32 r1, r2;
// }

// Intersection collider_intersection(Collider c1, c2) {
//     switch c1.shape {
//         case Sphere: switch c2.shape {
//             case Sphere: break;
//             case Point: break;
//             case Capsule: break;
//             case Cube: break;
//             case Triangle: break;
//         } break;
//         case Point: switch c2.shape {
//             case Point: break;
//             case Capsule: break;
//             case Cube: break;
//             case Triangle: break;
//             default: goto swap;
//         } break;
//         case Capsule: switch c2.shape {
//             case Capsule: break;
//             case Cube: break;
//             case Triangle: break;
//             default: goto swap;
//         } break;
//         case Cube: switch c2.shape {
//             case Cube: break;
//             case Triangle: break;
//             default: goto swap;
//         } break;
//         case Triangle: switch c2.shape {
//             case Triangle: break;
//             default: goto swap;
//         } break;
//     }

//     swap:
//     let inr = collider_intersection(c2, c1);
//     inr.normal = neg(inr.normal);
//     return inr;
// }


bool point_inside_bb(vec2 p, bb_min, bb_max) {
    return p.x < bb_max.x and p.y < bb_max.y and p.x > bb_min.x and p.y > bb_min.y;
}

struct Intersection {
    vec3 contact_point;
    vec3 normal;
    float32 depth;
}

vec3 closest_point_on_linesegment(vec3 a, b, p) {
    let ab = b - a;
    let t = clamp(dot(ab, p - a) / dot(ab, ab), 0, 1);
    return lerp(t, a, b);
}

vec2 closest_point_on_linesegment(vec2 a, b, p) {
    let ab = b - a;
    let t = clamp(dot(ab, p - a) / dot(ab, ab), 0, 1);
    return lerp(t, a, b);
}

/*
    capsules defined as:
        first: line segment ab with radius ab_r
        second: line segment cd with radius cd_r
*/
Intersection capsule_intersects(vec3 a, b, c, d, float32 ab_r, cd_r) {
    Intersection circle_depth(vec3 a, b, c, d, end_point, float32 ab_r, cd_r) {
        vec3 B = closest_point_on_linesegment(c, d, end_point);
        vec3 A = closest_point_on_linesegment(a, b, B);

        if false {
            gizmo_sphere(A, ab_r, Red);
            gizmo_sphere(B, cd_r, Red);
            gizmo_line(A, B, Yellow);
        }

        Intersection inr = {};
        inr.contact_point = B + (A - B)*0.5; // TODO: this is wrong for different radiuses
        inr.normal = normalize(A - B);
        inr.depth = ab_r + cd_r - dist(A, B);

        return inr;
    }

    let inr_a = circle_depth(a, b, c, d, a, ab_r, cd_r);
    let inr_b = circle_depth(a, b, c, d, b, ab_r, cd_r);

    return inr_a.depth < inr_b.depth ? inr_b : inr_a;
}

Intersection capsule_point_intersects(vec3 a, b, p, float32 radius) {
    vec3 A = closest_point_on_linesegment(a, b, p);

    Intersection inr = {};
    inr.normal = normalize(p - A);
    inr.contact_point = A + inr.normal*radius;
    inr.depth = radius - dist(A, p);

    return inr;
}

// Intersection capsule_sphere_intersects(vec3 a, b, p, float32 ab_r, p_r) {

// }


// triangle(abc) and point(p)
bool triangle_intersects(vec2 a,b,c,  p) {
    vec2 ab = rot90deg(b - a);
    vec2 bc = rot90deg(c - b);
    vec2 ca = rot90deg(a - c);

    bool b1 = dot(ab, p - a) < 0;
    bool b2 = dot(bc, p - b) < 0;
    bool b3 = dot(ca, p - c) < 0;

    return b1 and b2 and b3;
}

// moller trumbore
// ray: origin(o) direction(d)
// triangle(abc)
bool ray_triangle_intersects(vec3 o,d,  a,b,c, vec3* out_pos) {
    float32 epsilon = 0.0001;

    vec3 ab = b - a;
    vec3 ac = c - a;

    vec3 v_cross_ac = cross(d, ac);
    float32 det = dot(ab, v_cross_ac);

    if det > -epsilon and det < epsilon  return false;

    float32 inv_det = 1.0 / det;
    vec3 ao = o - a;

    float32 u = inv_det * dot(ao, v_cross_ac);
    if u < 0.0 or u > 1.0  return false;

    vec3 ao_cross_ab = cross(ao, ab);
    float32 v = inv_det * dot(d, ao_cross_ab);
    if v < 0.0 or u + v > 1.0  return false;

    float32 t = inv_det * dot(ac, ao_cross_ab);
    if t <= epsilon  return false;

    @out_pos = o + d*t;
    return true;
}

// https://en.wikipedia.org/wiki/Line%E2%80%93sphere_intersection
// TODO: this function is actually a line-sphere intersection test, not ray-sphere
bool ray_sphere_intersects(vec3 o,d,  c, float32 radius, float32* dist) {
    let delta = sq(dot(d, o - c)) - (sqlength(o - c) - sq(radius));
    @dist = -dot(d, o - c) - sqrtf(delta);
    return delta >= 0 and @dist >= 0;
}

// https://en.wikipedia.org/wiki/Line%E2%80%93plane_intersection
bool ray_plane_intersects(vec3 o,d,  p,n, vec3* out_point, float32* dist) {
    let enumerator = dot(p - o, n);
    let denominator = dot(d, n);

    if denominator == 0.0  return false; // line and plane are parallel
    if enumerator == 0.0  return false; // line is contained in plane

    @dist = enumerator / denominator;
    if @dist < 0.0  return false; // ray points in opposite direction of plane
    @out_point = o + d*@dist;
    return true;
}